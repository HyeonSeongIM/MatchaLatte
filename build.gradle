plugins {
    id 'java-library'
    id 'org.springframework.boot' apply(false)
    id 'io.spring.dependency-management'
    id 'io.spring.javaformat' apply(false)
    id 'org.asciidoctor.jvm.convert' apply(false)
}

apply from: 'lint.gradle'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    group = "${projectGroup}"
    version = "${applicationVersion}"
    sourceCompatibility = project.javaVersion

    repositories {
        mavenCentral()
    }

}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.asciidoctor.jvm.convert'
    apply plugin: 'jacoco'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependenciesVersion}"
        }
    }

    jacoco {
        toolVersion = "0.8.12"
    }

    def Qdomains = []
    for (qPattern in "**/QA".."**/QZ") {
        Qdomains.add(qPattern + "*")
    }

    def jacocoExcludePatterns = [
            "**/*Application*",
            "**/*Config*",
            "**/*Dto*",
            "**/*Request*",
            "**/*Response*",
            "**/*Interceptor*",
            "**/*Exception*",
            "**/entity/**",
            "**/test/**",
            "**/resources/**",
            "**/filter/**",
            "**/support/**",
            "**/handler/**",
            "**/request/**"
    ] + Qdomains

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    bootJar.enabled = false
    jar.enabled = true

    var snippetsDir = file("build/generated-snippets")

    test {
        useJUnitPlatform {
            excludeTags('restDocs')
        }
    }

    tasks.register('unitTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            excludeTags('context', 'restdocs')
        }
        finalizedBy 'jacocoTestReport'
    }

    tasks.register('contextTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('context')
        }
    }

    tasks.register('restDocs', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('restdocs')
        }
        outputs.dir snippetsDir
    }

    tasks.named('asciidoctor') {
        inputs.dir snippetsDir
        dependsOn test
    }

    tasks.named('checkFormatMain') {
        dependsOn tasks.named('compileJava')
    }

    tasks.named ('jacocoTestReport', JacocoReport) {
        dependsOn tasks.named('test')

        reports {
            html.required = true
            xml.required = true
        }

        classDirectories.setFrom(
                sourceSets.main.output.asFileTree.matching {
                    exclude jacocoExcludePatterns
                }
        )

        finalizedBy 'jacocoTestCoverageVerification'
    }

    tasks.named ('jacocoTestCoverageVerification', JacocoCoverageVerification) {

        // 검증 대상 소스 설정 (이 부분이 있어야 제대로 로드됨)
        classDirectories.setFrom(
                sourceSets.main.output.asFileTree.matching {
                    exclude jacocoExcludePatterns
                }
        )

        violationRules {
            rule {
                enabled = true
                element = 'CLASS'

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.00
                }
            }
        }
    }

}